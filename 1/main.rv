print_string:
	addi sp, sp, -0x10
	sw fp, 12(sp)
	addi fp, sp, 0x10
	sw ra, -8(fp)

	sw s2, -12(fp)

	addi s2, a0, 0
	print_string_loop:
		lbu a0, 0(s2)
		beq a0, x0, print_string_loop_exit

		call print_char

		addi s2, s2, 1
		jal x0, print_string_loop
	print_string_loop_exit:

	lw s2, -12(fp)

	lw ra, -8(fp)
	lw fp, -4(fp)
	addi sp, sp, 0x10
	jalr x0, ra, 0

print_s32:
	addi sp, sp, -0x20
	sw fp, 28(sp)
	addi fp, sp, 0x20
	sw ra, -8(fp)

	sw s2, -12(fp)
	sw s3, -16(fp)

	addi s2, a0, 0
	addi s3, fp, -0x11
	sb x0, 0(s3)

	bge s2, x0, print_int_loop
	addi a0, x0, 0x2D
	call print_char
	sub s2, x0, s2

	print_int_loop:
		addi s3, s3, -1
		addi a0, s2, 0
		addi a1, x0, 10
		call divmod
		addi s2, a0, 0
		addi a1, a1, 0x30
		sb a1, 0(s3)
		bne s2, x0, print_int_loop

	addi a0, s3, 0
	call print_string

	lw s3, -16(fp)
	lw s2, -12(fp)

	lw ra, -8(fp)
	lw fp, -4(fp)
	addi sp, sp, 0x20
	jalr x0, ra, 0

print_results:
	addi sp, sp, -0x10
	sw fp, 12(sp)
	addi fp, sp, 0x10
	sw ra, -8(fp)

	sw s1, -12(fp)
	sw s2, -16(fp)

	addi s1, a0, 0
	addi s2, a1, 0

	li a0, print_results_part1_prefix_string
	call print_string

	addi a0, s1, 0
	call print_s32

	li a0, print_results_part2_prefix_string
	call print_string

	addi a0, s2, 0
	call print_s32

	addi a0, x0, 0xA
	call print_char

	lw s1, -12(fp)
	lw s2, -16(fp)

	lw ra, -8(fp)
	lw fp, -4(fp)
	addi sp, sp, 0x10
	jalr x0, ra, 0
	
eat_u32:
	addi sp, sp, -0x20
	sw fp, 28(sp)
	addi fp, sp, 0x20
	sw ra, -8(fp)

	sw s1, -12(fp)
	sw s2, -16(fp)
	sw s3, -20(fp)
	sw s4, -24(fp)
	
	addi s1, a0, 0
	addi s2, x0, 0
	addi s4, x0, 10

	eat_u32_loop:
		lbu s3, 0(s1)
		addi s3, s3, -0x30
		bgeu s3, s4, eat_u32_loop_end
		
		addi a0, s2, 0
		addi a1, s4, 0
		call mul

		add s2, a0, s3

		addi s1, s1, 1
		jal x0, eat_u32_loop

	eat_u32_loop_end:

	addi a0, s1, 0
	addi a1, s2, 0

	lw s1, -12(fp)
	lw s2, -16(fp)
	lw s3, -20(fp)
	lw s4, -24(fp)

	lw ra, -8(fp)
	lw fp, -4(fp)
	addi sp, sp, 0x20
	jalr x0, ra, 0
	
eat_space:
	addi sp, sp, -0x10
	sw fp, 12(sp)
	addi fp, sp, 0x10
	sw ra, -8(fp)

	addi t1, x0, 32
	eat_space_loop:
		lbu t0, 0(a0)
		addi t0, t0, -1
		bgeu t0, t1, eat_space_loop_end
		addi a0, a0, 1
		jal x0, eat_space_loop
	eat_space_loop_end:

	lw ra, -8(fp)
	lw fp, -4(fp)
	addi sp, sp, 0x10
	jalr x0, ra, 0
	
quicksort_u32:
	addi t0, t0, 2
	bge a1, t0, quicksort_u32_main
	jalr x0, ra, 0
	quicksort_u32_main:

	addi sp, sp, -0x20
	sw fp, 28(sp)
	addi fp, sp, 0x20
	sw ra, -8(fp)

	sw s1, -12(fp)
	sw s2, -16(fp)
	sw s3, -20(fp)
	sw s4, -24(fp)

	addi s1, a0, 0
	addi s2, a1, 0

	addi t0, s2, -1
	slli t0, t0, 2
	add t0, s1, t0
	lw t0, 0(t0)
	addi t0, t0, 1

	addi t1, x0, 0
	addi s3, x0, 0
	quicksort_u32_pivot_loop:
		slli t2, t1, 2
		add t2, t2, s1
		lw t3, 0(t2)
		bge	t3, t0, quicksort_u32_pivot_loop_skip_swap

		slli t4, s3, 2
		add t4, t4, s1
		lw t5, 0(t4)
		sw t3, 0(t4)
		sw t5, 0(t2)
		addi s3, s3, 1

		quicksort_u32_pivot_loop_skip_swap:
		addi t1, t1, 1
		blt t1, s2, quicksort_u32_pivot_loop

	quicksort_u32_pivot_loop_end:

	addi a0, s1, 0
	addi a1, s3, -1
	call quicksort_u32

	addi a0, s3, 0
	slli a0, a0, 2
	add a0, a0, s1
	sub a1, s2, s3
	call quicksort_u32

	lw s1, -12(fp)
	lw s2, -16(fp)
	lw s3, -20(fp)
	lw s4, -24(fp)

	lw ra, -8(fp)
	lw fp, -4(fp)
	addi sp, sp, 0x20
	jalr x0, ra, 0

count_occurences:
	addi sp, sp, -0x20
	sw fp, 28(sp)
	addi fp, sp, 0x20
	sw ra, -8(fp)
	
	sw s1, -12(fp)
	sw s2, -16(fp)

	addi s1, a0, 0
	addi s2, a1, 0
	addi a0, x0, 0
	addi t0, x0, 0
	count_occurences_loop:
		slli t1, t0, 2
		add t1, t1, s1
		lw t1, 0(t1)

		bne t1, a2, count_occurences_loop_skip
		addi a0, a0, 1
		count_occurences_loop_skip:

		addi t0, t0, 1
		blt t0, s2, count_occurences_loop

	lw s1, -12(fp)
	lw s2, -16(fp)

	lw ra, -8(fp)
	lw fp, -4(fp)
	addi sp, sp, 0x20
	jalr x0, ra, 0

main:
	addi sp, sp, -0x20
	sw fp, 28(sp)
	addi fp, sp, 0x20
	sw ra, -8(fp)

	sw s1, -12(fp)
	sw s2, -16(fp)
	sw s3, -20(fp)
	sw s4, -24(fp)
	sw s5, -28(fp)
	sw s6, -32(fp)

	li s1, input
	li s2, a_buffer
	li s3, b_buffer
	addi s4, x0, 0

	main_parse_loop:
		addi a0, s1, 0
		call eat_u32
		addi s1, a0, 0
		sw a1, 0(s2)
		addi s2, s2, 4

		addi a0, s1, 0
		call eat_space
		addi s1, a0, 0

		addi a0, s1, 0
		call eat_u32
		addi s1, a0, 0
		sw a1, 0(s3)
		addi s3, s3, 4

		addi s4, s4, 1

		addi s1, s1, 2 # skip line break
		lbu a0, 0(s1)
		bne a0, x0, main_parse_loop

	li a0, a_buffer
	addi a1, s4, 0
	call quicksort_u32

	li a0, b_buffer
	addi a1, s4, 0
	call quicksort_u32

	li s1, a_buffer
	li s2, b_buffer
	addi s3, x0, 0
	addi s5, x0, 0
	main_sum_loop:
		slli t1, s5, 2
		add t2, t1, s1
		lw t2, 0(t2)
		add t3, t1, s2
		lw t3, 0(t3)

		sub t1, t3, t2
		bge t1, x0, main_sum_loop_skip_neg
		sub t1, x0, t1
		main_sum_loop_skip_neg:

		add s3, s3, t1

		addi s5, s5, 1
		blt s5, s4, main_sum_loop

	li t0, part1_result
	sw s3, 0(t0)

	li s1, a_buffer
	li s2, b_buffer
	addi s3, x0, 0
	addi s6, x0, 0
	main_occ_loop:
		slli s5, s3, 2
		add s5, s5, s1
		lw s5, 0(s5)

		addi a0, s2, 0
		addi a1, s4, 0
		addi a2, s5, 0
		call count_occurences

		addi a1, s5, 0
		call mul

		add s6, s6, a0

		addi s3, s3, 1
		blt s3, s4, main_occ_loop

	li a0, part1_result
	lw a0, 0(a0)
	addi a1, s6, 0
	call print_results

	lw s1, -12(fp)
	lw s2, -16(fp)
	lw s3, -20(fp)
	lw s4, -24(fp)
	lw s5, -28(fp)
	lw s6, -32(fp)

	lw ra, -8(fp)
	lw fp, -4(fp)
	addi sp, sp, 0x20
	jalr x0, ra, 0

print_results_part1_prefix_string: "Part 1 result: " 0
print_results_part2_prefix_string: 0xA "Part 2 result: " 0
a_buffer: rep8 4000 0
b_buffer: rep8 4000 0
part1_result: 0x00000000
